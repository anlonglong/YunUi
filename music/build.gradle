if (rootProject.ext.isIntegrationModel) {
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}

def androidCfg = rootProject.ext.android//引用config.gradle文件中的Android数组下面的值

android {

    compileSdkVersion androidCfg.compileSdkVersion
    buildToolsVersion androidCfg.buildToolsVersion

    defaultConfig {
        if (!rootProject.ext.isIntegrationModel) {
            applicationId "demo.lon.com.music"
        }
        minSdkVersion androidCfg.minSdkVersion
        targetSdkVersion androidCfg.targetSdkVersion
        versionCode androidCfg.versionCode
        versionName androidCfg.versionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        if (rootProject.ext.isIntegrationModel) {
            javaCompileOptions {
                annotationProcessorOptions {
                    arguments = [moduleName: project.getName()]
                }
            }
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            if (rootProject.ext.isIntegrationModel) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
                java.srcDirs = ['src/main/java', 'src/main/module/java']
                res.srcDirs = ['src/main/res', 'src/main/module/res']
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }

        }
    }
    packagingOptions {
        exclude 'META-INF/ASL2.0';
        exclude 'META-INF/DEPENDENCIES.txt';
        exclude 'META-INF/LICENSE.txt';
        exclude 'META-INF/NOTICE.txt';
        exclude 'META-INF/NOTICE';
        exclude 'META-INF/LICENSE';
        exclude 'META-INF/DEPENDENCIES';
        exclude 'META-INF/notice.txt';
        exclude 'META-INF/license.txt';
        exclude 'META-INF/dependencies.txt';
        exclude 'META-INF/LGPL2.1';
        exclude 'META-INF/services/javax.annotation.processing.Processor';
        exclude 'META-INF/rxjava.properties';
    }

}





dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':commonlibrary')

//    implementation 'com.android.support:appcompat-v7:26.1.0'
//    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
}
